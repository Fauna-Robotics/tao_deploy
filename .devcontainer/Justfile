container_name := "tao-deploy"
workspace_dir := "/workspace/fauna"

[private]
default:
  @just --list

# Start the container and open VSCode in the container
start: up code

# Start the container
up *args="":
  devcontainer up \
    --workspace-folder {{ shell("git rev-parse --show-toplevel") }} \
    --config devcontainer.json \
    {{ args }}

# Start the container, removing the previous container if it exists
recreate *args="":
  just up --remove-existing-container {{ args }}

# Stop the container
stop:
  docker stop {{ container_name }}

# Remove the container
rm: stop
  docker rm {{ container_name }}

# Open VSCode in the container
code:
  #!/usr/bin/env bash
  json="{\"containerName\":\"{{ container_name }}\"}"
  encoded=$(echo "$json" | xxd -p -c10000)
  code --folder-uri vscode-remote://attached-container+$encoded/{{ workspace_dir }}
